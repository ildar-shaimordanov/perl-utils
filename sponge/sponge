#!/usr/bin/env perl

=head1 NAME

sponge - soak up standard input and write to a file

=head1 SYNOPSIS

    sed '...' file | grep '...' | sponge [-a] file

=head1 DESCRIPTION

sponge is Perl version of the sponge from the Debian package moreutils. 

It reads standard input to memory and writes it out to the specified file. 
Unlike a shell redirect, the script soaks up all its input before opening 
the output file. This allows constructing pipelines that read from and 
write to the same file. If no file is specified, outputs to STDOUT. 

=head1 OPTIONS

=over 4

=item B<--version>

Print the program version and exit.

=item B<-a>, B<--append>

Append to the file.

=back

=head1 SEE ALSO

http://joeyh.name/code/moreutils/

http://backreference.org/2011/01/29/in-place-editing-of-files/

=head1 COPYRIGHT

Copyright 2014-2021, Ildar Shaimordanov

  MIT License

=cut

# =========================================================================

use strict;
use warnings;

use Pod::Usage;
use Getopt::Long qw( :config auto_version );

our $VERSION = "0.2";

my $append = 0;

GetOptions("a|append" => \$append) or pod2usage;

pod2usage({ -verbose => 2, -noperldoc => 1 }) if -t 0;

# =========================================================================

my $file = shift @ARGV || "-";
my @lines = <STDIN>;

open(OUT, ( $append and ">>" or ">" ) . $file) or die "sponge: cannot open $file: $!\n";
print OUT @lines;
close(OUT);

# =========================================================================

# EOF
